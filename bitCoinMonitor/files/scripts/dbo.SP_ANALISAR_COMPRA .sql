CREATE PROCEDURE SP_ANALISAR_COMPRA (@IDT_CONSULTA INT) 
AS
 BEGIN

	DECLARE @MRC_OPERAR CHAR(1)
	DECLARE @VLR_LIMITE DECIMAL(18,5)
	DECLARE @VLR_MAX_COMPRA DECIMAL(18,5)
	DECLARE @VLR_MIN_VENDA DECIMAL(18,5)
	DECLARE @DAT_COMPRA_ANT DATETIME
	DECLARE @DAT_VENDA_ANT DATETIME
	DECLARE @VLR_INVESTIDO_ANT DECIMAL(18,5)
	DECLARE @VLR_VENDIDO_ANT DECIMAL(18,5)
	DECLARE @IDT_CONSULTA_VENDA_ANT INT
	DECLARE @FLAG VARCHAR(10)
	DECLARE @CENARIO VARCHAR(10)

	SET @FLAG = 'OPERAR'

	--Caso exista, pega os dados da COMPRA anterior
	SELECT	@DAT_COMPRA_ANT = O.DAT_CRIACAO,
			@VLR_INVESTIDO_ANT = O.QTD_EXECUTADA * O.VLR_MEDIO_EXECUTADO
	FROM	TB_MINHAS_ORDENS O INNER JOIN 
			(
			SELECT	MAX(DAT_CRIACAO) DAT_CRIACAO
			FROM	TB_MINHAS_ORDENS
			WHERE	IDT_TIPO_ORDEM = 'C'
			) D
				ON(	D.DAT_CRIACAO = O.DAT_CRIACAO)
	WHERE	O.IDT_TIPO_ORDEM = 'C'


	--Caso exista, pega os dados da última VENDA
	SELECT	@DAT_VENDA_ANT = O.DAT_CRIACAO,
			@VLR_VENDIDO_ANT = (O.QTD_EXECUTADA * O.VLR_MEDIO_EXECUTADO)-O.VLR_TAXA,
			@IDT_CONSULTA_VENDA_ANT = IDT_CONSULTA_ORIGEM
	FROM	TB_MINHAS_ORDENS O INNER JOIN 
			(
			SELECT	MAX(DAT_CRIACAO) DAT_CRIACAO
			FROM	TB_MINHAS_ORDENS
			) D
				ON(	D.DAT_CRIACAO = O.DAT_CRIACAO)
	WHERE	O.IDT_TIPO_ORDEM = 'V'

	--Se na última compra teve prejuízo significa que a venda foi forçada para não ter um prejuízo maior (possível queda da cotação).
	--Nessa situação iremos suspender as compras
	IF(@DAT_VENDA_ANT IS NOT NULL AND @DAT_COMPRA_ANT IS NOT NULL)
	 BEGIN
		IF(@DAT_VENDA_ANT > @DAT_COMPRA_ANT AND @VLR_VENDIDO_ANT/@VLR_INVESTIDO_ANT-1 < 0 ) SET @FLAG = 'SUSPENDER'
	 	--//Se acabou de vender, espera um 1 dia para voltar a negociar
		IF( DATEADD(DAY,1,@DAT_VENDA_ANT) > GETDATE()) SET @FLAG = 'ABORTAR'
	 END

	SELECT	@CENARIO = CENARIO_COMPRA
	FROM	VW_CENARIO
	WHERE	idt_consulta = @IDT_CONSULTA


	--//Verificando se deixa de suspender e realiza a compra forçada (na cotação baixa)
	IF(@FLAG = 'SUSPENDER') 
	 BEGIN					
		SELECT	@FLAG =  CASE WHEN ( E.TENDENCIA_MEDIA_COMPRA = 'SUBINDO' AND
									 E.TENDENCIA_MINIMA_COMPRA = 'SUBINDO' AND
									 E.TENDENCIA_MEDIA_VENDA = 'SUBINDO' AND
									 E.TENDENCIA_MAX_VENDA = 'SUBINDO' AND
									 E.TENDENCIA_QTD_COMPRA = 'SUBINDO' AND
									 E.TENDENCIA_QTD_VENDA = 'SUBINDO' AND
 									 E.QTD_COMPRA_LIVRO >  E.QTD_VENDA_LIVRO AND
									 @CENARIO = 'CAINDO' AND
									 V.CENARIO_COMPRA = 'CAINDO' AND
									  C.vlr_maximo - C.vlr_minimo > 2000 AND
									 (C.vlr_compra - C.vlr_minimo) > 1000 ) 
							  THEN 'COMPRAR' ELSE 'SUSPENDER' END 
		FROM	VW_ESTATISTICAS_FINAL E JOIN TB_CONSULTA C
					ON(	C.IDT_CONSULTA = E.IDT_CONSULTA)
		JOIN	VW_CENARIO_CURTO V
					ON(V.idt_consulta = C.idt_consulta)
		WHERE	E.IDT_CONSULTA = @IDT_CONSULTA

	 END
	  
	IF(@FLAG = 'OPERAR' OR @FLAG = 'COMPRAR')
	 BEGIN
		
		SET @MRC_OPERAR = 'N'
		

		IF( @FLAG = 'COMPRAR') 
			SET @MRC_OPERAR = 'S'
		ELSE IF( @CENARIO = 'CAINDO' )
		 BEGIN								 			
			
			SELECT	@MRC_OPERAR =  CASE WHEN (	DIF_COMPRA_X_VENDA >= P.vlr_dif_compra_x_venda AND
												QTD_COMPRA_LIVRO >  QTD_VENDA_LIVRO AND
												DISTANCIA_MAXIMO < P.pct_ditancia_compra_max AND
												TENDENCIA_MEDIA_COMPRA = 'SUBINDO' AND
												V.CENARIO_COMPRA =  'CAINDO' AND
												(C.vlr_maximo - C.vlr_minimo) > P.vlr_dif_max_min
												) THEN 'S' ELSE 'N' END 
			FROM	VW_ESTATISTICAS_FINAL E JOIN TB_PARAMETROS P
						ON(1=1)
			JOIN	TB_CONSULTA C
						ON(	C.IDT_CONSULTA = E.IDT_CONSULTA)
			JOIN	VW_CENARIO_CURTO V
						ON(	V.idt_consulta = C.idt_consulta)
			WHERE	E.IDT_CONSULTA = @IDT_CONSULTA
		
		 END
		ELSE IF ( @CENARIO = 'SUBINDO' )
		 BEGIN
			SELECT	@FLAG =  CASE WHEN ( E.TENDENCIA_MEDIA_COMPRA = 'SUBINDO' AND
										 E.TENDENCIA_MINIMA_COMPRA = 'SUBINDO' AND
										 E.TENDENCIA_MEDIA_VENDA = 'SUBINDO' AND
										 E.TENDENCIA_MAX_VENDA = 'SUBINDO' AND
										 E.TENDENCIA_QTD_COMPRA = 'SUBINDO' AND
										 E.TENDENCIA_QTD_VENDA = 'SUBINDO' AND
 										 E.QTD_COMPRA_LIVRO >  E.QTD_VENDA_LIVRO AND
										 C.vlr_maximo - C.vlr_minimo > 2000 AND
										 (C.vlr_compra - C.vlr_minimo) > 1000 ) 
								  THEN 'COMPRAR' ELSE 'SUSPENDER' END 
			FROM	VW_ESTATISTICAS_FINAL E JOIN TB_CONSULTA C
						ON(	C.IDT_CONSULTA = E.IDT_CONSULTA)
			WHERE	E.IDT_CONSULTA = @IDT_CONSULTA

			SET @MRC_OPERAR = CASE WHEN @FLAG = 'COMPRAR' THEN 'S' ELSE 'N' END

		 END
			
		IF(@MRC_OPERAR = 'S')
		 BEGIN
		
			--Pegando a cotação mais cara de compra
			SELECT	@VLR_MAX_COMPRA = MAX(VLR_PRECO_LIMITE)
			FROM	TB_LIVRO_ORDENS
			WHERE	IDT_CONSULTA = @IDT_CONSULTA
			AND		COD_TIPO_ORDEM = 'C'
		
			--//Pegando a cotação mais barata de venda
			SELECT	@VLR_MIN_VENDA = MIN(VLR_PRECO_LIMITE)
			FROM	TB_LIVRO_ORDENS
			WHERE	IDT_CONSULTA = @IDT_CONSULTA
			AND		COD_TIPO_ORDEM = 'V'	
	 
			IF(@VLR_MIN_VENDA = @VLR_MAX_COMPRA) 
				SET @VLR_LIMITE = @VLR_MIN_VENDA + 0.01
			ELSE
				SET @VLR_LIMITE = @VLR_MIN_VENDA - 0.01
	 
		 END
	 END

	SELECT	ISNULL(@MRC_OPERAR,'N') MRC_OPERAR,
			ISNULL(@VLR_LIMITE,0) VLR_LIMITE,
			CASE WHEN (@FLAG = 'COMPRAR') THEN 'S' ELSE 'N' END MRC_AGUARDAR

 END